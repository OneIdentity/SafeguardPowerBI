let
    IsMocked = Extension.ImportFunction("IsMocked", "Utils.pqm"),
    InfoLog = Extension.ImportFunction("InfoLog", "Utils.pqm"),
    Request.GetAuthParameters = (encodedCredentails as text) as record =>
        let
            parameters = [
                RelativePath = "authentication",
                Query = [return = "/api/authentication"],
                Headers = [
                    #"Authorization" = Text.Combine({"Basic ", encodedCredentails}),
                    #"Accept" = "*/*",
                    #"Connection" = "keep-alive",
                    #"Cache-Control" = "no-cache, no-store, must-revalidate"
                ]
            ]
        in
            parameters,
    Request.GetDefaultParameters = (path as text, sessionId as text) as record =>
        let
            parameters = [
                RelativePath = path,
                Headers = [
                    #"Accept" = "application/json",
                    #"Connection" = "keep-alive",
                    #"Cookie" = sessionId
                ],
                IsRetry = true
            ]
        in
            parameters,
    Request.Generate = (url as text, parameters as record, optional mocks as record) as table =>
        let
            requestUrl = InfoLog("Base url of the http request", url),
            requestParameters = InfoLog("Request parameters of the http request", parameters),
            response = if IsMocked(mocks, "Response") then mocks[Response] else Web.Contents(url, parameters),
            _meta = InfoLog("Response meta data of the http request", Value.Metadata(response)),
            _json = InfoLog("Response data of the http request", Json.Document((response))),
            _table = Table.FromRecords({_json})
        in
            Value.ReplaceMetadata(_table, _meta),
    Request.GetHeader = (requestMetaData as record, headerName as text) as text =>
        Record.Field(requestMetaData[Headers], headerName)
in
    [
        GetAuthParameters = Request.GetAuthParameters,
        GetDefaultParameters = Request.GetDefaultParameters,
        Generate = Request.Generate,
        GetHeader = Request.GetHeader
    ]

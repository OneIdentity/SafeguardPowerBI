let
    Logger.ErrorLog = Extension.ImportFunction("ErrorLog", "Logger.pqm"),
    RequestErrors.ErrorMap = Extension.ImportFunction("ErrorMap", "RequestErrors.pqm"),
    RequestErrors.NotParsableResponse = Extension.ImportFunction("NotParsableResponse", "RequestErrors.pqm"),
    ResponseHandler.GetDataFromResponse = (response as record, path as text, optional separator as text) =>
        let
            GetRecordSafe = (content as record, pathAsList as list) =>
                let
                    currentField = List.First(pathAsList),
                    currentContent =
                        if currentField <> -1 then
                            Record.FieldOrDefault(content, currentField, null) meta Value.Metadata(response)
                        else
                            content,
                    handledContent =
                        if currentContent = null then
                            RequestErrors.NotParsableResponse(
                                [
                                    Cause = response,
                                    MissingField = path,
                                    RequestUrl = Value.Metadata(response)[RequestUrl]
                                ]
                            )
                        else
                            currentContent,
                    nextPathAsList = List.RemoveFirstN(pathAsList, 1),
                    result =
                        if List.Count(nextPathAsList) > 0 then
                            @GetRecordSafe(handledContent, nextPathAsList)
                        else
                            handledContent
                in
                    result,
            _separator = separator ?? ".",
            pathAsList = Text.Split(path, _separator),
            data = GetRecordSafe(response, pathAsList)
        in
            data,
    ResponseHandler.ValidateByStatusCode = (response as record) =>
        let
            responseMeta = Value.Metadata(response),
            httpStatus = Text.From(responseMeta[Response.Status]),
            log = Logger.ErrorLog(
                "SPS returned with an error response",
                [
                    Response = response,
                    Response.Status = responseMeta[Response.Status],
                    RequestUrl = responseMeta[RequestUrl]
                ]
            ),
            details = [
                Cause = response,
                RequestUrl = log[RequestUrl]
            ]
        in
            if Record.HasFields(RequestErrors.ErrorMap, httpStatus) then
                Record.Field(RequestErrors.ErrorMap, httpStatus)(details)
            else
                response
in
    [
        GetDataFromResponse = ResponseHandler.GetDataFromResponse,
        ValidateByStatusCode = ResponseHandler.ValidateByStatusCode
    ]

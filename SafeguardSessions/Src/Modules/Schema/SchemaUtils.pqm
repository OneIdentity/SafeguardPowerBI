let
    IsMocked = Extension.ImportFunction("IsMocked", "CommonUtils.pqm"),
    PaddedTable.FromRecords = Extension.ImportFunction("PaddedTable.FromRecords", "CommonUtils.pqm"),
    SchemaTransformationErrors.ListToTextConversionError = Extension.ImportFunction(
        "ListToTextConversionError", "SchemaTransformationErrors.pqm"
    ),
    SchemaTransformationErrors.SchemaApplyError = Extension.ImportFunction(
        "SchemaApplyError", "SchemaTransformationErrors.pqm"
    ),
    Logger.ErrorLog = Extension.ImportFunction("ErrorLog", "CommonLogger.pqm"),
    SchemaUtils.NameColumn = "Name",
    SchemaUtils.TypeColumn = "Type",
    SchemaUtils.Header = {SchemaUtils.NameColumn, SchemaUtils.TypeColumn},
    SchemaUtils.HandleUnexpectedSchemaApplyError = (input as record, response as record) =>
        if input[HasError] = true then
            let
                log = Logger.ErrorLog("An error happened when applying schema", [
                    Error = input[Error]
                ]),
                withResponse = log & [Response = response],
                converted = SchemaTransformationErrors.SchemaApplyError(
                    [
                        Cause = withResponse[Response],
                        RequestUrl = Value.Metadata(response)[RequestUrl]
                    ]
                )
            in
                converted
        else
            input[Value],
    SchemaUtils.CreateSchema = (fields as list) => #table(SchemaUtils.Header, fields),
    SchemaUtils.ConvertListToText = (l as nullable list, fieldName as text) as nullable text =>
        let
            asText =
                if l is list then
                    try
                        Text.Combine(l, ",")
                    otherwise
                        let
                            log = Logger.ErrorLog(
                                "List cannot be converted to text as it contains other types than text",
                                [
                                    FieldName = fieldName,
                                    Value = l
                                ]
                            ),
                            converted = SchemaTransformationErrors.ListToTextConversionError(
                                [
                                    Cause = Text.FromBinary(Json.FromValue(log[Value])),
                                    RequestUrl = null
                                ]
                            )
                        in
                            converted
                else
                    l
        in
            asText,
    SchemaUtils.GetSessionItemsResponse = (response as nullable record) as list =>
        let
            defaultValue = {},
            transformedResponse =
                if response <> null then
                    let
                        GetItems = (response as record) as list =>
                            let
                                TransformOperation = (sessions_list as list) =>
                                    List.Transform(sessions_list, each [session_id = [key]] & [body])
                            in
                                if Record.HasFields(response, "items") then
                                    Record.TransformFields(response, {"items", TransformOperation})[items]
                                else
                                    defaultValue,
                        ApplyMeta = (items as list, response as record) as list =>
                            if Record.HasFields(response, "meta") then
                                Value.ReplaceMetadata(items, response[#"meta"])
                            else
                                items,
                        items = GetItems(response),
                        withMeta = ApplyMeta(items, response)
                    in
                        withMeta
                else
                    defaultValue
        in
            transformedResponse,
    SchemaUtils.FieldNamesOfType = (_table as table, t as type) as list =>
        let
            fieldNamesOfType =
                if Table.RowCount(_table) = 0 then
                    {}
                else
                    List.Select(
                        Table.ColumnNames(_table),
                        (fieldName) =>
                            Type.Is(Value.Type(List.First(List.RemoveNulls(Table.Column(_table, fieldName)))), t)
                    )
        in
            List.Buffer(fieldNamesOfType),
    SchemaUtils.IsFlat = (_table as table) as logical =>
        List.AllTrue(
            {
                List.IsEmpty(SchemaUtils.FieldNamesOfType(_table, Record.Type)),
                List.IsEmpty(SchemaUtils.FieldNamesOfType(_table, List.Type))
            }
        ),
    SchemaUtils.FlattenLists = (asTable as table, fieldNames as list) as table =>
        let
            flattened = Table.Buffer(
                Table.TransformColumns(
                    asTable,
                    List.Transform(
                        fieldNames, (fieldName) =>
                            {fieldName, (row) => SchemaUtils.ConvertListToText(row, fieldName)}
                    )
                )
            )
        in
            flattened,
    SchemaUtils.FlattenRecords = (asTable as table, fieldNames as list) as table =>
        let
            ExpandRecord = (asTable as table, fieldName as text) as any =>
                let
                    namesToExpand = List.Distinct(
                        Record.FieldNames(Record.Combine(List.RemoveNulls(Table.Column(asTable, fieldName))))
                    ),
                    newNames = List.Transform(
                        namesToExpand, (nameToExpand) => Text.Combine({fieldName, nameToExpand}, ".")
                    ),
                    expanded = Table.ExpandRecordColumn(asTable, fieldName, namesToExpand, newNames)
                in
                    Table.Buffer(expanded),
            flattened = List.Accumulate(
                fieldNames, asTable, (asTable, fieldName) => ExpandRecord(asTable, fieldName)
            )
        in
            Table.Buffer(flattened),
    SchemaUtils.Flatten = (listOfRecords as list, optional columnsToRemove as list) as table =>
        let
            asTable = PaddedTable.FromRecords(listOfRecords),
            reducedTable = Table.RemoveColumns(
                asTable, if columnsToRemove <> null then columnsToRemove else {}, MissingField.Ignore
            ),
            flattenedSubTable =
                if SchemaUtils.IsFlat(reducedTable) then
                    Table.Buffer(reducedTable)
                else
                    let
                        // Flatten lists
                        listFieldNames = SchemaUtils.FieldNamesOfType(reducedTable, List.Type),
                        withFlattenedLists = SchemaUtils.FlattenLists(reducedTable, listFieldNames),
                        // Flatten records
                        recordFieldNames = SchemaUtils.FieldNamesOfType(reducedTable, Record.Type),
                        withFlattenedRecords = SchemaUtils.FlattenRecords(withFlattenedLists, recordFieldNames)
                    in
                        @SchemaUtils.Flatten(Table.ToRecords(withFlattenedRecords), columnsToRemove)
        in
            flattenedSubTable,
    SchemaUtils.GetResponseWithSchema = (_table as table, schema as table) as table =>
        let
            EnforceTypes = (_table as table, schema as table) as table =>
                // Applies type transforms to a given table
                let
                    map = (t) =>
                        // Determines type transforms
                        if List.AnyTrue(
                            {Type.Is(t, type list), Type.Is(t, type record), Type.Is(t, type table), t = type any}
                        ) then
                            null
                        else
                            t,
                    // Add missing columns
                    missingColumns = List.Difference(
                        Table.Column(schema, SchemaUtils.NameColumn), Table.ColumnNames(_table)
                    ),
                    extraColumns = List.Difference(
                        Table.ColumnNames(_table), Table.Column(schema, SchemaUtils.NameColumn)
                    ),
                    identicalTable = Table.Combine(
                        {Table.RemoveColumns(_table, extraColumns), #table(missingColumns, {})}
                    ),
                    // The columns will be ordered according to the schema table
                    withReordered = Table.ReorderColumns(
                        identicalTable, Table.Column(schema, SchemaUtils.NameColumn)
                    ),
                    mapped = Table.TransformColumns(schema, {SchemaUtils.TypeColumn, map}),
                    omitted = Table.SelectRows(mapped, each Table.Column(_, SchemaUtils.TypeColumn) <> null),
                    primitiveTransforms = Table.ToRows(omitted),
                    // Apply type transform
                    changedPrimitives = Table.TransformColumnTypes(withReordered, primitiveTransforms)
                in
                    changedPrimitives,
            // Returns the table type for a given schema
            SchemaToTableType = (schema as table) as type =>
                let
                    toList = List.Transform(
                        Table.Column(schema, SchemaUtils.TypeColumn),
                        (t) => Record.FromList({t, false}, {SchemaUtils.TypeColumn, "Optional"})
                    ),
                    toRecord = Record.FromList(toList, Table.Column(schema, SchemaUtils.NameColumn)),
                    toType = Type.ForRecord(toRecord, false)
                in
                    type table (toType),
            enforcedTypes = EnforceTypes(_table, schema),
            withType = Value.ReplaceType(enforcedTypes, SchemaToTableType(schema))
        in
            withType,
    SchemaUtils.ApplySchema = (response as record, schema as table, optional mocks as nullable record) =>
        let
            transformedResponse = SchemaUtils.GetSessionItemsResponse(response),
            flattenedResponse = SchemaUtils.Flatten(transformedResponse, {"vault"}),
            withSchema =
                if IsMocked(mocks, "ApplySchema") then
                    mocks[ApplySchema]
                else
                    SchemaUtils.GetResponseWithSchema(flattenedResponse, schema),
            processedResponse = SchemaUtils.HandleUnexpectedSchemaApplyError(try (withSchema), response),
            withMeta = Value.ReplaceMetadata(processedResponse, Value.Metadata(response))
        in
            withMeta
in
    [
        CreateSchema = SchemaUtils.CreateSchema,
        GetSessionItemsResponse = SchemaUtils.GetSessionItemsResponse,
        FieldNamesOfType = SchemaUtils.FieldNamesOfType,
        IsFlat = SchemaUtils.IsFlat,
        FlattenLists = SchemaUtils.FlattenLists,
        FlattenRecords = SchemaUtils.FlattenRecords,
        Flatten = SchemaUtils.Flatten,
        GetResponseWithSchema = SchemaUtils.GetResponseWithSchema,
        ApplySchema = SchemaUtils.ApplySchema
    ]

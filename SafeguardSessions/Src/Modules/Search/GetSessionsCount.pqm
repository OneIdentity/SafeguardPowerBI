let
    GetOption = SafeguardSessions.ImportFunction("GetOption", "CommonUtils.pqm"),
    SetOption = SafeguardSessions.ImportFunction("SetOption", "CommonUtils.pqm"),
    SessionsCountEndpoint = SafeguardSessions.ImportFunction("SessionsCountEndpoint", "Constants.pqm"),
    Request.Generate = SafeguardSessions.ImportFunction("Generate", "Request.pqm"),
    Request.GetDefaultParameters = SafeguardSessions.ImportFunction("GetDefaultParameters", "Request.pqm"),
    ResponseHandler.GetDataFromResponse = SafeguardSessions.ImportFunction(
        "GetDataFromResponse", "CommonResponseHandler.pqm"
    ),
    ResponseHandler.ValidateByStatusCode = SafeguardSessions.ImportFunction(
        "ValidateByStatusCode", "ResponseHandler.pqm"
    ),
    UrlBuilder.GenerateUrl = SafeguardSessions.ImportFunction("GenerateUrl", "UrlBuilder.pqm"),
    GetSessionsCount.GetSessionsCount = (
        spsUrl as text, queryParams as record, sessionId as text, optional options as nullable record
    ) as number =>
        let
            sessionsCountUrl = UrlBuilder.GenerateUrl(spsUrl, SessionsCountEndpoint, queryParams),
            countResponse = ResponseHandler.ValidateByStatusCode(
                Request.Generate(
                    sessionsCountUrl,
                    Request.GetDefaultParameters(sessionId),
                    SetOption([], "Mocks.Response", GetOption(options, "Mocks.GetSessionsCountResponse"))
                )
            ),
            count = ResponseHandler.GetDataFromResponse(countResponse, "count")
        in
            count
in
    [
        GetSessionsCount = GetSessionsCount.GetSessionsCount
    ]

let  
    ValidateByStatusCode = Extension.ImportFunction("ValidateByStatusCode", "ResponseHandler.pqm"),
    InfoLog = Extension.ImportFunction("InfoLog", "CommonLogger.pqm"),
    ErrorCodes = Extension.ImportFunction("ErrorCodes", "RequestErrors.pqm"),

    //Executes query against SPP API with specified host target, headers, path, and body (content). 
    //
    //Returns JSON response.
    Request.GetSppResponse = (host as text, headers as record, optional relativePath as text, optional content as binary, optional query as record) =>
        let
            options = [
                Headers = headers,
                RelativePath = relativePath,
                Content = content,
                Query = query
            ],

            loggedParameters = Record.RemoveFields(options,"Headers"),

            log = InfoLog("Request sent to SPP", [Url = host, Parameters = loggedParameters]),

            response = Web.Contents(log[Url], options & [ManualStatusHandling = {400}]),
            json = InfoLog("Response from SPP: ", Json.Document(response)),
            extendedMeta = Value.Metadata(response) & [RequestUrl = host] & [RelativePath = relativePath],
            responseWithMeta = Value.ReplaceMetadata(json, extendedMeta)
        in
            ValidateByStatusCode(responseWithMeta)
in
    [
        GetSppResponse = Request.GetSppResponse
    ]
